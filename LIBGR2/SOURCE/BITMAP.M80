        name Bitmap

        cseg

        public vidPutSprite


        extrn  vidGetAddr



$include (:f1:video2.i80)



;------------------------------------------------------------------------
; Формат спрайта:
; --------------
; 8 байт - битовый образ 8x8
; 8 байт - цвет фона и пикселей для каждой строки образа
;------------------------------------------------------------------------


;------------------------------------------------------------------------
; Вывод битмапа в заданные координаты
;------------------------------------------------------------------------
; на входе:
;    [SP+2]     - x
;    C          - y
;    DE         - адрес спрайта
vidPutSprite:
        pop     H
        xthl                    ; L - x
        ; L     - x
        ; C     - y
        ; DE    - bitmap
        push    D
        mov     E, C
        mov     C, L
        call    vidGetAddr
        pop     D
        ; HL    - vidmem[x, y]
        ; DE    - bitmap
        ; переносим битовый образ
        mvi     C, 8
  @putbmp:                       ; цикл вывода одного символа
        ldax    D
        mov     M, A
        inx     D
        inx     H
        dcr     C
        jnz     @putbmp
        ; теперь цвет
        lxi     B, -8
        dad     B
        mov     A, H
        xri     20h
        mov     H, A            ; HL - адрес в массиве цвета
        mvi     C, 8
    @putcol:
        ldax    D
        mov     M, A
        inx     H
        inx     D
        dcr     C
        jnz     @putcol

        ret
end
